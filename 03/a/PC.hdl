// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], inc, load, reset;
    OUT out[16];
    
    PARTS:
    Register(load=true, in=newVal, out=out, out=rOut);
    Mux16(sel=reset, a=others1, b=false, out=newVal);
    Mux16(sel=load, a=others2, b=in, out=others1);
    Mux16(sel=inc, a=rOut, b=incremented, out=others2);
    Inc16(in=rOut, out=incremented);
}
