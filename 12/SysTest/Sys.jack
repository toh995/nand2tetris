// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {
  static Array errorCodes;

  /** Performs all the initializations required by the OS. */
  function void init() {
    do Memory.init();
    do Keyboard.init();
    do Math.init();
    do Output.init();
    do Screen.init();
    do Sys._initErrorCodes();
    do Main.main();
    return;
  }

  /** Halts the program execution. */
  function void halt() {
    while (true) {}
    return;
  }

  /** Waits approximately duration milliseconds and returns.  */
  function void wait(int duration) {
    var int i, iStop;
    var int j, jStop;
    if (duration < 0) { do Sys.error(1); }
    let i = 0;
    let iStop = duration * 10000;
    let jStop = 125;
    while (i < iStop) {
      let j = 0;
      while (j < jStop) {
        let j = j + 1;
      }
      let i = i + 1;
    }
    return;
  }

  /** Displays the given error code in the form "ERR<errorCode>",
   *  and halts the program's execution. */
  function void error(int errorCode) {
    do Output.moveCursor(0, 0);
    do Output.printString("ERR");
    do Output.printInt(errorCode);
    do Output.printString(errorCodes[errorCode]);
    return;
  }

  function void _initErrorCodes() {
    let errorCodes = Array.new(21);
    let errorCodes[1] = "Duration must be positive";
    let errorCodes[2] = "Array size must be positive";
    let errorCodes[3] = "Division by zero";
    let errorCodes[4] = "Cannot compute square root of a negative number";
    let errorCodes[5] = "Allocated memory size must be positive";
    let errorCodes[6] = "Heap overflow";
    let errorCodes[7] = "Illegal pixel coordinates";
    let errorCodes[8] = "Illegal line coordinates";
    let errorCodes[9] = "Illegal rectangle coordinates";
    let errorCodes[12] = "Illegal center coordinates";
    let errorCodes[13] = "Illegal radius";
    let errorCodes[14] = "Maximum length must be non-negative";
    let errorCodes[15] = "String index out of bounds";
    let errorCodes[16] = "String index out of bounds";
    let errorCodes[17] = "String is full";
    let errorCodes[18] = "String is empty";
    let errorCodes[19] = "Insufficient string capacity";
    let errorCodes[20] = "Illegal cursor location";
    return;
  }
}
